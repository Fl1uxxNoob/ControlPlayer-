plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'net.fliuxx'
version = '1.1'

sourceCompatibility = '15'
targetCompatibility = '15'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = "JDA"
        url = "https://m2.dv8tion.net/releases"
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    implementation 'net.dv8tion:JDA:5.0.0-beta.6'
    implementation 'org.xerial:sqlite-jdbc:3.42.0.0'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Copy) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filteringCharset = 'UTF-8'
        filter { line ->
            line.replace('@version@', project.version.toString())
                    .replace('@name@', project.name)
        }
    }
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

shadowJar {
    archiveClassifier.set('') // sovrascrive il jar standard
    mergeServiceFiles()
}

build.dependsOn shadowJar